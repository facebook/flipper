/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.19"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    ndkVersion rootProject.ndkVersion
    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        applicationId 'com.facebook.flipper.sample'
        targetSdkVersion rootProject.targetSdkVersion
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile './AndroidManifest.xml'
        }

        test {
            java {
                exclude 'com/facebook/flipper/plugins/facebook/**'
            }
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

    // Android Support Library
    implementation deps.supportAppCompat
    implementation deps.supportRecyclerView

    // Litho
    implementation deps.lithoCore
    implementation deps.lithoWidget
    implementation deps.lithoAnnotations
    implementation deps.lithoFresco
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor deps.lithoProcessor

    // Third-party
    implementation deps.soloader
    implementation deps.okhttp3
    implementation deps.fresco
    implementation deps.protobuf
    implementation deps.datastorePreferences

    // Integration test
    androidTestImplementation deps.testCore
    androidTestImplementation deps.testRules

    // Unit tests
    testImplementation deps.mockito
    testImplementation deps.robolectric
    testImplementation deps.hamcrest
    testImplementation deps.junit

    debugImplementation project(':android')
    debugImplementation project(':fresco-plugin')
    debugImplementation project(':network-plugin')
    debugImplementation project(':litho-plugin')
    debugImplementation project(':datastore-viewer')
    releaseImplementation project(':noop')
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.18.0"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
